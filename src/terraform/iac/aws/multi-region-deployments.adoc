= Terraform - AWS: Multi-region deployments

The following configuration deploys a single server in a single region.

[source,hcl]
----
provider "aws" {
  region = "us-west-1"
}

data "aws_ami" "defaut_latest_ubuntu20" {
  owners      = ["099720109477"]
  most_recent = true
  filter {
    name   = "name"
    values = ["ubuntu/images/hvm-ssd/ubuntu-focal-20.04-amd64-server-*"]
  }
}

resource "aws_instance" "my_default_server" {
  instance_type = "t3.micro"
  ami           = data.aws_ami.defaut_latest_ubuntu20.id
  tags = {
    Name = "Default Server"
  }
}
----

What if we wanted to deploy additional servers, but into specific regions? This is common in large organizations that have a global presence and therefore need to maintain a multi-region infrastructure.

To do this, first we need to define multiple "providers", each with a different region. The `alias` attribute is what will allow us to differentiate between these different configurations for the AWS provider.

[source,hcl]
----
provider "aws" {
  region = "us-west-1"
}

provider "aws" {
  region = "eu-south-1"
  alias  = "EUROPE"
}

provider "aws" {
  region = "ap-northeast-1"
  alias  = "ASIA"
}
----

Then, we can use these providers to create resources in the specified regions. By default, a resource that does not specify `provider`, will use the default provider configuration, which is the one without an `alias`.

[source,hcl]
----
# Will be launched into us-west-1.
resource "aws_instance" "my_default_server" {
  instance_type = "t3.micro"
  ami           = data.aws_ami.default_latest_ubuntu20.id
  tags = {
    Name = "Default Server"
  }
}

# Will be launched into eu-south-1.
resource "aws_instance" "my_europe_server" {
  provider      = aws.EUROPE
  instance_type = "t3.micro"
  ami           = data.aws_ami.europe_latest_ubuntu20.id
  tags = {
    Name = "Europe Server"
  }
}

# Will be launched into ap-northeast-1.
resource "aws_instance" "my_asia_server" {
  provider      = aws.ASIA
  instance_type = "t3.micro"
  ami           = data.aws_ami.asia_latest_ubuntu20.id
  tags = {
    Name = "Asia Server"
  }
}
----

Each server will be launched in its respective region, using the latest Ubuntu 20.04 AMI _available in that region_, which is configured in the following `data` blocks:

[source,hcl]
----
data "aws_ami" "default_latest_ubuntu20" {
  owners      = ["099720109477"]
  most_recent = true
  filter {
    name   = "name"
    values = ["ubuntu/images/hvm-ssd/ubuntu-focal-20.04-amd64-server-*"]
  }
}

data "aws_ami" "europe_latest_ubuntu20" {
  provider    = aws.EUROPE
  owners      = ["099720109477"]
  most_recent = true
  filter {
    name   = "name"
    values = ["ubuntu/images/hvm-ssd/ubuntu-focal-20.04-amd64-server-*"]
  }
}

data "aws_ami" "asia_latest_ubuntu20" {
  provider    = aws.ASIA
  owners      = ["099720109477"]
  most_recent = true
  filter {
    name   = "name"
    values = ["ubuntu/images/hvm-ssd/ubuntu-focal-20.04-amd64-server-*"]
  }
}
----
