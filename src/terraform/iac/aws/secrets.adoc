= Terraform - AWS: Secrets

Using the AWS Provider, you can generate, store, and retrieve secrets using two services:

* SSM Parameter Store
* AWS Secrets Manager

== SSM Parameter Store

The following configuration defines an RDS instance:

[source]
----
provider "aws" {
  region = "ca-central-1"
}

resource "aws_db_instance" "prod" {
  identifier           = "prod-mysql-rds"
  allocated_storage    = 20
  storage_type         = "gp2"
  engine               = "mysql"
  engine_version       = "5.7"
  instance_class       = "db.t3.micro"
  parameter_group_name = "default.mysql5.7"
  skip_final_snapshot  = true
  apply_immediately    = true
  username             = "administrator"
  password             = data.aws_ssm_parameter.rds_password.value
}
----

Notice the `password` argument. This is referencing a parameter stored in SSM Parameter Store. Let's create some configuration to generate the password and store it in SSM Parameter Store.

This first block is used to generate a random password.

[source]
----
resource "random_password" "main" {
  length           = 20
  special          = true     /* Default: !@#$%&*()-_=+[]{}<>:? */
  override_special = "#!()_"  /* Overrides the default list of allowed special characters. */
}
----

This next block takes the `result` from the `random_password.main` resource and stores it in SSM Parameter Store.

[source]
----
resource "aws_ssm_parameter" "rds_password" {
  /* The name serves as the location (path) of the parameter in the store. */
  name        = "/prod/prod-mysql-rds/password"
  description = "Master Password for RDS Database"
  type        = "SecureString"
  value       = random_password.main.result
}
----

This next block retrieves the password from SSM Parameter Store. Notice the use of the `depends_on` argument. This is used to ensure that password is created before this block is executed.

[source]
----
data "aws_ssm_parameter" "rds_password" {
  name       = "/prod/prod-mysql-rds/password"
  depends_on = [aws_ssm_parameter.rds_password]
}
----

Now the value of the password can be referenced via the expression `data.aws_ssm_parameter.rds_password.value`. This is used to set the password in the `aws_db_instance` resource, above.

We can output the values of the RDS instance using the `output` block. The `rds_password` output is marked as `sensitive`, which means it will not be displayed in the console output when running `terraform apply`. This is a good practice for sensitive information like passwords.

[source]
----
output "rds_address" {
  value = aws_db_instance.prod.address
}

output "rds_port" {
  value = aws_db_instance.prod.port
}

output "rds_username" {
  value = aws_db_instance.prod.username
}

output "rds_password" {
  value     = data.aws_ssm_parameter.rds_password.value
  sensitive = true
}
----

== AWS Secrets Manager

AWS RECOMMENDS storing access credentials such as access keys and passwords in AWS Secrets Manager, rather than the Parameter Store. Secrets Manager is a paid service, while Parameter Store is free. Secrets Manager provides additional features such as automatic rotation of secrets, which is not available in Parameter Store.

[source]
----
provider "aws" {
  region = "ca-central-1"
}

resource "aws_db_instance" "prod" {
  identifier           = "prod-mysql-rds"
  allocated_storage    = 20
  storage_type         = "gp2"
  engine               = "mysql"
  engine_version       = "5.7"
  instance_class       = "db.t3.micro"
  parameter_group_name = "default.mysql5.7"
  skip_final_snapshot  = true
  apply_immediately    = true
  username             = "administrator"
  password             = data.aws_secretsmanager_secret_version.rds_password.secret_string
}

/* Generate password - as before. */
resource "random_password" "main" {
  length           = 20
  special          = true
  override_special = "#!()_"
}

/* Store password in Secrets Manager. This block defines a new
entry in Secrets Manager, but does not yet assign a value. */
resource "aws_secretsmanager_secret" "rds_password" {
  name                    = "/prod/rds/password"
  description             = "Password for my RDS Database"

  /* This setting means that, if you delete a secret, you will not be
  able to restore it. By default, in Secrets Manager, deleted secrets
  can be recovered. */
  recovery_window_in_days = 0
}

/* This adds the generated password to the store. */
resource "aws_secretsmanager_secret_version" "rds_password" {
  secret_id     = aws_secretsmanager_secret.rds_password.id
  secret_string = random_password.main.result
}

/* Retrieve the stored password. Depends on the password
having been created first. This local data is then used in an
expression and the output value assigned to the `password`
argument in the RDS resource block, above. */
data "aws_secretsmanager_secret_version" "rds_password" {
  secret_id  = aws_secretsmanager_secret.rds_password.id
  depends_on = [aws_secretsmanager_secret_version.rds_password]
}
----

When you `terraform apply` this, you should see the secret added to the AWS Secrets Manager console.

Here's how you could store all details of the RDS instance in Secrets Manager.

[source]
----
resource "aws_secretsmanager_secret" "rds" {
  name                    = "/prod/rds/all"
  description             = "All Details for my RDS Database"
  recovery_window_in_days = 0
}

resource "aws_secretsmanager_secret_version" "rds" {
  secret_id = aws_secretsmanager_secret.rds.id

  /* Encode all the properties you want to store as JSON. */
  secret_string = jsonencode({
    rds_address  = aws_db_instance.prod.address
    rds_port     = aws_db_instance.prod.port
    rds_username = aws_db_instance.prod.username
    rds_password = random_password.main.result
  })
}
----

If we wanted to output this data, we'd need to decode it again:

[source]
----
/* Capture the secret from the store in local data variable. */
data "aws_secretsmanager_secret_version" "rds" {
  secret_id  = aws_secretsmanager_secret.rds.id
  depends_on = [aws_secretsmanager_secret_version.rds]
}

output "rds_all" {
  value = nonsensitive(jsondecode(data.aws_secretsmanager_secret_version.rds.secret_string))
}
----

Alternatively, you can output the individual values, by referencing the keys in the JSON object.

[source]
----
output "rds_address" {
  value = jsondecode(data.aws_secretsmanager_secret_version.rds.secret_string)["rds_address"]
}

output "rds_port" {
  value = jsondecode(data.aws_secretsmanager_secret_version.rds.secret_string)["rds_port"]
}

output "rds_username" {
  value = jsondecode(data.aws_secretsmanager_secret_version.rds.secret_string)["rds_username"]
}

output "rds_password" {
  value     = jsondecode(data.aws_secretsmanager_secret_version.rds.secret_string)["rds_password"]
  sensitive = true
}
----
