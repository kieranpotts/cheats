= Terraform: Remote state and back-ends

By default, when you `apply` a Terraform configuration, the state file is stored locally in the current working directory as `terraform.tfstate`. When you make subsequent changes to the configuration, Terraform will determine the changes required by comparing the new configuration with the existing local state file.

Best practice is to store the state file remotely, so it can be shared among team members and also used in CI/CD pipelines.

However, because secrets are store in the state file, the state file SHOULD NOT be committed to source control (even if the repository is private).

To store your state files in other remote, secure locations, you configure an alternative https://developer.hashicorp.com/terraform/language/state/backends[back-end] (the default back-end is your local filesystem).

Options for backends include S3 and GCS (Google Cloud Storage) buckets.

You can also use multiple state files, for example splitting your infrastructure into separate components which can be managed independently. This is particularly useful for large enterprise-scale infrastructures, or in any context in which different teams are responsible for different parts of the infrastructure. Alternatively you might want to split your infrastructure state by layers, such as separating the networking layer from the application layer – this is another common practice.

Back-ends are defined in the `terraform` block, in the `backend` sub-block. The following example shows how to configure a back-end for storing the state file in an S3 bucket. It also defines a VPC, and internet gateway attached to the VPC, two public subnets, and associating the same route table configuration (which routes all traffic to the internet gateway) to each subnet – all in the `eu-north-1` region.

[IMPORTANT]
======
If using buckets to store remote state, the buckets MUST be private and their data MUST be encrypted at rest. This is to ensure that sensitive information, such as passwords and access keys, which may be captured in the Terraform state files, are not exposed to unauthorized users.
======

.variables.tf
[source,hcl]
----
variable "vpc_cidr" {
  default = "10.0.0.0/16"
}

variable "env" {
  default = "Dev"
}

variable "public_subnet_cidrs" {
  default = [
    "10.0.1.0/24",
    "10.0.2.0/24"
  ]
}
----

.main.tf
[source,hcl]
----
provider "aws" {
  region = "eu-north-1"
}

data "aws_availability_zones" "available" {}

terraform {
  backend "s3" {
    bucket = "terraform-remote-state-abcdef" // Bucket where to save Terraform state
    key    = "dev/network/terraform.tfstate" // Object name in the bucket to save Terraform state
    region = "us-west-2"                     // Region where bucket created. This does NOT need
                                             //   to match where the configured infrastructure is
                                             //   deployed.
  }
}

resource "aws_vpc" "main" {
  cidr_block = var.vpc_cidr
  tags = {
    Name  = "${var.env}-vpc"
  }
}

resource "aws_internet_gateway" "main" {
  vpc_id = aws_vpc.main.id
  tags = {
    Name  = "${var.env}-igw"
  }
}

resource "aws_subnet" "public_subnets" {
  count                   = length(var.public_subnet_cidrs)
  vpc_id                  = aws_vpc.main.id
  cidr_block              = element(var.public_subnet_cidrs, count.index)
  availability_zone       = data.aws_availability_zones.available.names[count.index]
  map_public_ip_on_launch = true
  tags = {
    Name  = "${var.env}-public-${count.index + 1}"
  }
}

resource "aws_route_table" "public_subnets" {
  vpc_id = aws_vpc.main.id
  route {
    cidr_block = "0.0.0.0/0"
    gateway_id = aws_internet_gateway.main.id
  }
  tags = {
    Name  = "${var.env}-route-public-subnets"
  }
}

resource "aws_route_table_association" "public_routes" {
  count          = length(aws_subnet.public_subnets[*].id)
  route_table_id = aws_route_table.public_subnets.id
  subnet_id      = element(aws_subnet.public_subnets[*].id, count.index)
}
----

.outputs.tf
[source,hcl]
----
output "vpc_id" {
  value = aws_vpc.main.id
}

output "vpc_cidr" {
  value = aws_vpc.main.cidr_block
}

output "public_subnet_ids" {
  value = aws_subnet.public_subnets[*].id
}
----

This configuration provides a basic network setup that can be used for deploying applications in the cloud. This is our "network layer". We can then create another Terraform configuration that uses this network layer, and deploys applications in the public subnets created by this configuration. This is our "application layer".

The following configuration creates a single web server in one of the public subnets created by the previous configuration. It uses the remote state to retrieve the VPC ID, public subnet IDs, and CIDR range for the VPC. It also creates a security group that allows HTTP and SSH traffic to the web server.

.variables.tf
[source,hcl]
----
variable "env" {
  default = "Dev"
}
----

.main.tf
[source,hcl]
----
provider "aws" {
  region = "eu-north-1"
}


terraform {
  backend "s3" {
    bucket = "adv-it-terraform-remote-state"
    key    = "dev/webserver/terraform.tfstate" // Different path than the state file
                                               // for the network layer
    region = "us-west-2"
  }
}

// This block gets data from the remote state file for the "network layer",
// which we can then use to configure our resources. Notice how we are using
// our outputs from the network layer's state file to get the VPC ID, public
// subnet IDs, and VPC CIDR range.
data "terraform_remote_state" "network_layer" {
  backend = "s3"
  config = {
    bucket = "terraform-remote-state-abcdef"
    key    = "dev/network/terraform.tfstate"
    region = "us-west-2"
  }
}

data "aws_ami" "latest_amazon_linux" {
  owners      = ["amazon"]
  most_recent = true
  filter {
    name   = "name"
    values = ["amzn2-ami-hvm-*-x86_64-gp2"]
  }
}

resource "aws_instance" "web_server" {
  ami                    = data.aws_ami.latest_amazon_linux.id
  instance_type          = "t3.micro"
  vpc_security_group_ids = [aws_security_group.webserver.id]

  // Here we are using the remote state data to get the first public subnet ID.
  subnet_id              = data.terraform_remote_state.network_layer.outputs.public_subnet_ids[0]

  user_data              = <<EOF
#!/bin/bash
yum -y update
yum -y install httpd
myip=`curl http://169.254.169.254/latest/meta-data/local-ipv4`
echo "<h2>WebServer with IP: $myip</h2><br>Build by Terraform with Remote State"  >  /var/www/html/index.html
service httpd start
chkconfig httpd on
EOF
  tags = {
    Name  = "${var.env}-WebServer"
  }
}

resource "aws_security_group" "webserver" {
  name   = "WebServer Security Group"

  // Here wer are using the remote state data to get the VPC ID.
  vpc_id = data.terraform_remote_state.network_layer.outputs.vpc_id

  ingress {
    from_port   = 80
    to_port     = 80
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  ingress {
    from_port   = 22
    to_port     = 22
    protocol    = "tcp"

    // Here we are using the remote state data to get the VPC CIDR range.
    cidr_blocks = [data.terraform_remote_state.network_layer.outputs.vpc_cidr]
  }
  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = {
    Name  = "${var.env}-web-server-sg"
  }
}
----

.outputs.tf
[source,hcl]
----
output "web_server_public_ip" {
  value = aws_instance.web_server.public_ip
}
----
