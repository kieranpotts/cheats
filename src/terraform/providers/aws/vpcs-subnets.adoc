= Terraform: AWS - VPCs, subnets, and route tables

As an absolute minimum, a default VPC must be defined. This is required to create subnets and security group, which cross-references the VPCs to which they belong.

== VPCs

.main.tf
[source,hcl]
----
resource "aws_default_vpc" "default" {}
----

The default VPC is created automatically in each AWS region, but it still must be explicitly referenced in the Terraform config. This is necessary to retrieve the VPC ID, which is required for security groups and subnets.

.main.tf
[source,hcl]
----
resource "aws_security_group" "app_sg" {
  name   = "HTTP"
  vpc_id = aws_vpc.my_test_vpc.id

  ingress {
    // ...
  }

  egress {
    // ...
  }
}
----

More commonly, rather than using the default VPC, a custom VPC is created. This is done by defining a `aws_vpc` resource. The VPC is created in the region specified in the provider block. CIDR ranges should be defined for custom VPCs, and tags may be applied to VPCs.

.main.tf
[source,hcl]
----
resource "aws_vpc" "my_test_vpc" {
  cidr_block = "10.0.0.0/16"

  tags = {
    Name = "MyTestVPC"
  }
}
----

CIDR ranges and tags are good candidates to extract from infrastructure configuration to variables.

.terraform.tfvars
----
# VPC
vpc_cidr = "10.0.0.0/16"
vpc_name = "MyTestVPC"
----

.main.tf
[source,hcl]
----
resource "aws_vpc" "my_test_vpc" {
  cidr_block = var.vpc_cidr

  tags = {
    Name = var.vpc_name
  }
}
----

== Subnets

Subnets are created within VPCs.

.terraform.tfvars
----
# Public Subnet
subnet_cidr = "10.0.1.0/24"
subnet_name = "MyTestSubnet"
----

.main.tf
[source,hcl]
----
resource "aws_subnet" "my_test_subnet" {
  vpc_id = aws_vpc.my_test_vpc.id
  cidr_block = var.subnet_cidr

  tags = {
    Name = var.subnet_name
  }
}
----

== Internet Gateways

An Internet Gateway (IGW) is required to route traffic from public subnets to and from the Internet.

.terraform.tfvars
----
# Internet Gateway
igw_name = "MyTestIGW"
----

.main.tf
[source,hcl]
----
resource "aws_internet_gateway" "my_ig" {
  vpc_id = aws_vpc.my_test_vpc.id

  tags = {
    Name = var.igw_name
  }
}
----

== Route tables

Route tables are added to VPCs to define how traffic is routed. The following configuration routes all traffic to the Internet Gateway. Here, the route table is assigned the same name tag as the IGW.

.main.tf
[source,hcl]
----
resource "aws_route_table" "public_rt" {
  vpc_id = aws_vpc.my_test_vpc.id

  route {
    cidr_block = "0.0.0.0/0"
    gateway_id = aws_internet_gateway.my_ig.id
  }

  tags = {
    Name = var.igw_name
  }
}
----

To complete the configuration, the route table must be associated with the subnet. This is done by creating a `aws_route_table_association` resource, which maps a subnet to a route table.

.main.tf
[source,hcl]
----
resource "aws_route_table_association" "public_1_rt_assoc" {
  subnet_id      = aws_subnet.my_test_subnet.id
  route_table_id = aws_route_table.public_rt.id
}
----
