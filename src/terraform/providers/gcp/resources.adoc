= Terraform - GCP: Resources

.Simple example of a GCP resource
[source,hcl]
----
resource "google_compute_instance" "my_server" {
  name         = "my-gcp-server"
  machine_type = "f1-micro"

  boot_disk {
    initialize_params {
      /* Image to use for the VM. */
      image = "debian-cloud/debian-10"
    }
  }

  network_interface {
    /* This will enable a private IP address. */
    network = "default"

    /* This will enable a public IP address. */
    access_config {}
  }
}
----

The following example creates an Apache web server running on Ubuntu.

.Web server example
[source,hcl]
----
resource "google_project_service" "api" {
  /* Enable two APIs: Compute Engine and Cloud Resource Manager. */
  for_each = toset([
    "cloudresourcemanager.googleapis.com",
    "compute.googleapis.com"
  ])
  disable_on_destroy = false
  service            = each.value
}

resource "google_compute_firewall" "web" {
  name          = "web-access"
  network       = "default"
  source_ranges = ["0.0.0.0/0"]
  allow {
    protocol = "tcp"
    ports    = ["80", "443"]
  }
}

resource "google_compute_instance" "my_web_server" {
  name         = "my-gcp-web-server"
  machine_type = "f1-micro"
  boot_disk {
    initialize_params {
      image = "debian-cloud/debian-9"
    }
  }
  network_interface {
    network = "default"
    access_config {}
  }
  metadata_startup_script = <<EOF
#!/bin/bash
apt update -y
apt install apache2 -y
echo "<h2>WebServer on GCP Build by Terraform!<h2>"  >  /var/www/html/index.html
systemctl restart apache2
EOF

  /* Specify that this resource depends on the API and firewall
  resources, defined above. */
  depends_on = [google_project_service.api, google_compute_firewall.web]
}
----
